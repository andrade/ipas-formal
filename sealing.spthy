/**
** Protocol: IPAS-Sealing
** Author:   Daniel Andrade
** Date:     2022
** Status:   Working
**/

theory Sealing
begin

builtins: symmetric-encryption

functions: session_key/1 [private], sealing_key/1 [private], MAC/2

/**
** Model AEAD where
**     encryption(plaintext, associated data, nonce, secret key) = ciphertext
**     decryption(ciphertext, associated data, nonce, secret key) = plaintext
**/
functions: aead_enc/4, aead_dec/4, aead_ver/4, true/0
equations:
	aead_dec(aead_enc(m, a, n, k), a, n, k) = m,
	aead_ver(aead_enc(m, a, n, k), a, n, k) = true // REVIEW

/**
** Client writes the client encrypted data plus the
** sealed client encryption key to persistent memory.
** Persistent memory is untrusted.
**
** We model this by having the client sending m to storage
** via public network which is available to the adversary.
**/
rule Storage:
	let
		m = <c, a>
	in
	[
		In(m)
	]--[
		Storage(m)
	]->[
	]

rule Client_1 [color=#00ffff]:
	let
		sk = session_key($sk)
		m = senc(<~k, ~nonce>, sk)
	in
	[
		Fr(~k),
		Fr(~nonce)
	]--[
		OUT_I_1(m),
		Progress_A1($Alice, m)
	]->[
		AState($Alice, ~k, ~nonce),
		Out(m)
	]

rule Client_2 [color=#00ffff]:
	let
		mk = session_key($mk)
		i = <sealed_data, MAC(<sealed_data, ~nonce>, mk)>

		a = <~n, sealed_data>
		c = aead_enc(~d, a, ~n, ~k)
		m = <c, a>
	in
	[
		AState($Alice, ~k, ~nonce),
		In(i),
		Fr(~d),
		Fr(~n)
	]--[
		Progress_A2($Alice, i, m),
		Secret(~k)
	]->[
		Out(m) // send to network instead of saving to disk
	]

rule Server [color=#d3d3d3]:
	let
		sk = session_key($sk)
		m = senc(<k, nonce>, sk)

		// self_key = sealing_key($server)
		self_key = ~x // assegurar instância única (self_key only needed here)
		sealed_data = senc(k, self_key)

		mk = session_key($mk)
		o = <sealed_data, MAC(<sealed_data, nonce>, mk)>
	in
	[
		Fr(~x), // assegurar instância única
		In(m)
	]--[
		IN_R_1(k, m),
		Progress_S1($CSS, m, o),
		Secret(k)
	]->[
		Out(o)
	]

/**
** Whenever responder receives the client data encryption key, K,
** either K was known to adversary or initiator sent m1 beforehand.
**/
lemma types [sources]:
	"
	All k m #i.
	IN_R_1(k, m) @ #i ==> (
		(Ex #j. KU(k) @ #j & #j < #i)
		| (Ex #j. OUT_I_1(m) @ #j)
	)
	"

lemma flow: exists-trace
	"Ex A B m1 m2 m3 #t1 #t2 #t3 #t4.
	Progress_A1(A, m1) @ t1 &
	Progress_S1(B, m1, m2) @ t2 &
	Progress_A2(A, m2, m3) @ t3 &
	Storage(m3) @ t4"/* &

	t1 < t2 &
	t2 < t3 &
	t3 < t4"*/

lemma secrecy:
	"All x #i #j. Secret(x) @i & K(x) @j ==> F"

end
